#!/usr/bin/perl

our ($TOOLHOME, $PLUGINSDIR, $CONFDIR, $LOGDIR, $RUNDIR);
BEGIN { 
	$0 =~ m"^(.*)/";
	$TOOLHOME = "$1/..";
	while( $TOOLHOME =~ s{(^|/)[^\.][^/]*/\.\.(/|$)}{$1} ){}
	$TOOLHOME =~ s{/$}{};
	$PLUGINSDIR = "$TOOLHOME/plugins";
	$CONFDIR = "$TOOLHOME/conf";
	$LOGDIR = "$TOOLHOME/log";
	$RUNDIR = "$TOOLHOME/run";
}
use lib "$TOOLHOME/lib/perl5";

use strict;
use JSON;
use Time::Local;
use Encode;
use Carp::Always;
use Getopt::Long qw(:config posix_default no_ignore_case gnu_compat);
use Foucault::Common;
use Foucault::Configuration;
use Foucault::Filter;
use Foucault::Monitor;
use Foucault::WellKnown;
use Foucault::Diagnosis;

our $JSON_CODEC        = JSON->new;
our $JSON_CODEC_PRETTY = JSON->new->pretty->canonical;

####

our $UTF8_CODEC;
sub utf8safe ($) {
	my ($text) = @_;
	$UTF8_CODEC = find_encoding "utf-8" unless defined $UTF8_CODEC;
	eval {
		my $tmp = $text;
		my $utf8 = $UTF8_CODEC->decode($tmp, 1);
	};
	if( $@ ){
		s{[\x80-\xff]}{'$'.uc(unpack('H2',$&))}eg;
	}
	return $text;
}

####

sub cleanup_dir ($) {
	my ($dir) = @_;
	opendir my $d, $dir or do {
		die "$dir: cannot open, stopped";
	};
	my @f;
	while( my $e = readdir $d ){
		next if $e =~ m"^\.";
		next unless -f "$dir/$e";
		push @f, $e;
	}
	close $d;
	foreach my $e (@f){ unlink "$dir/$e"; }
}

sub listup_dir ($) {
	my ($dir) = @_;
	opendir my $d, $dir or do {
		die "$dir: cannot open, stopped";
	};
	my @f;
	while( my $e = readdir $d ){
		next if $e =~ m"^\.";
		next unless -f "$dir/$e";
		push @f, $e;
	}
	close $d;
	return @f;
}

#### Sub Commands

sub subcmd_create_statsdir ($) {
	my ($dir) = @_;
	mkdir "$dir";
	mkdir "$dir/inputs";
	mkdir "$dir/samples";
	mkdir "$dir/additional_samples";
	mkdir "$dir/1_concatination_setting";
	mkdir "$dir/1-1_collection_by_tag";
	mkdir "$dir/1-2_concatinated_messages";
	mkdir "$dir/1-3_buffered_messages";
	mkdir "$dir/2_monitoring_setting";
	mkdir "$dir/2-1_collection_by_pattern";
	mkdir "$dir/2-2_unmonitored_messages";
	mkdir "$dir/3_detection_setting";
	mkdir "$dir/3-1_detected_anomalies";
	mkdir "$dir/3-2_detected_events";
	mkdir "$dir/3-3_redirected_messages";
	mkdir "$dir/4_classification_setting";
	mkdir "$dir/4-1_generalized";
	mkdir "$dir/5_tabulation_setting";
	mkdir "$dir/5-1_grouped_events";
	mkdir "$dir/5-2_grouped_generalized";
	mkdir "$dir/5-3_cross_tables";
	mkdir "$dir/5-4_classified_by_equivalence";

	my $conf = Foucault::Configuration->new( $TOOLHOME );
	$conf->read_foucault03d_conf;
	my $wellknowndir             = $conf->get_path_of_wellknowndir;
	my $concatfilter_conf_path   = $conf->get_path_of_concatfilter_conf;
	my $anomalymonitor_conf_path = $conf->get_path_of_anomalymonitor_conf;
	my $report_conf_path         = $conf->get_path_of_report_conf;
	system "cp -p \"$concatfilter_conf_path\"    \"$dir/1_concatination_setting\"";
	system "cp -p \"$anomalymonitor_conf_path\"  \"$dir/2_monitoring_setting\"";
	system "cp -p \"$report_conf_path\"          \"$dir/5_tabulation_setting\"";

	$conf->read_anomalymonitor_conf;
	my @patterns = $conf->get_anomalymonitor_patterns;
	system "cp -p \"$wellknowndir/\"*.regexp            \"$dir/3_detection_setting\"";
	system "cp -p \"$wellknowndir/\"template_of_regexp  \"$dir/3_detection_setting\"";
	system "cp -p \"$wellknowndir/\"*.rules             \"$dir/4_classification_setting\"";
	system "cp -p \"$wellknowndir/\"template_of_rules   \"$dir/4_classification_setting\"";
	system "cp -p \"$wellknowndir/\"*.samples           \"$dir/samples\"";
	system "cp -p \"$wellknowndir/\"template_of_samples \"$dir/samples\"";
}

sub subcmd_cleanup_statsdir ($) {
	my ($dir) = @_;
	cleanup_dir "$dir/1-1_collection_by_tag";
	cleanup_dir "$dir/1-2_concatinated_messages";
	cleanup_dir "$dir/1-3_buffered_messages";
	cleanup_dir "$dir/2-1_collection_by_pattern";
	cleanup_dir "$dir/2-2_unmonitored_messages";
	cleanup_dir "$dir/3-1_detected_anomalies";
	cleanup_dir "$dir/3-2_detected_events";
	cleanup_dir "$dir/3-3_redirected_messages";
	cleanup_dir "$dir/4-1_generalized";
	cleanup_dir "$dir/5-1_grouped_events";
	cleanup_dir "$dir/5-2_grouped_generalized";
	cleanup_dir "$dir/5-3_cross_tables";
	cleanup_dir "$dir/5-4_classified_by_equivalence";
}

sub subcmd_compile ($$$$$) {
	my ($confdir, $rulesdir, $samplesdir, $additional_samplesdir, $regexpdir) = @_;
	print "compiling.";
	my $conffile  = "$confdir/anomalymonitor.conf";
	my $conf      = Foucault::Configuration->new;
	my $wellknown = Foucault::WellKnown->new;

	$conf->read_anomalymonitor_conf( $conffile );
	my @patterns = $conf->get_anomalymonitor_patterns;
	foreach my $pattern ( @patterns ){
		print ".";

		$wellknown->set_path_of_wellknowndir( $regexpdir );
		unless( $wellknown->read_patternregexp_file_of($pattern) ){
			print "creating $pattern regexp.";
			$wellknown->create_patternregexp_file_of($pattern);
		}
		$wellknown->set_path_of_wellknowndir( $rulesdir );
		unless( $wellknown->read_generalizerules_file_of($pattern) ){
			print "creating $pattern rules.";
			$wellknown->create_generalizerules_file_of($pattern);
		}
		$wellknown->set_path_of_wellknowndir( $samplesdir );
		$wellknown->read_samples_file_of    ( $pattern );
		$wellknown->set_path_of_wellknowndir( $additional_samplesdir );
		$wellknown->read_samples_file_of    ( $pattern );
		unless( $wellknown->get_mtime_of_samples_of($pattern) ){
			print "creating $pattern samples.";
			$wellknown->set_path_of_wellknowndir( $samplesdir );
			$wellknown->create_samples_file_of($pattern);
		}

		my $patterbregexp_mtime   = $wellknown->get_mtime_of_patternregexp_of($pattern);
		my $generalizerules_mtime = $wellknown->get_mtime_of_generalizeregexp_of($pattern);
		my $samples_mtime         = $wellknown->get_mtime_of_samples_of($pattern);

		next unless defined $samples_mtime;
		next if $patterbregexp_mtime >= $samples_mtime && $patterbregexp_mtime >= $generalizerules_mtime;

		$wellknown->compile( $pattern );

		$wellknown->set_path_of_wellknowndir    ( $regexpdir );
		$wellknown->write_patternregexp_file_of ( $pattern );
		$wellknown->write_diagnosis_file_of     ( $pattern );
	}
	print "done.\n";
}

sub subcmd_sort_by_tag ($$) {
	my ($inputdir, $outputdir) = @_;
	print "sorting by tag.";
	my %out_fh;
	my @list = listup_dir $inputdir;
	foreach my $f ( @list ){
		print ".";
		open my $in_fh, "<", "$inputdir/$f" or die;
		while( my $in = <$in_fh> ){
			next if $in =~ m"^\s*$";
			my %in_hash = json2hash $in;
			my $tag = $in_hash{tag};

			my $out_fh = $out_fh{$tag};
			unless( defined $out_fh ){
				open $out_fh, ">>", "$outputdir/$tag.ndjson" or die;
				$out_fh{$tag} = $out_fh;
			}
			print $out_fh $in;
		}
		close $in_fh;
	}
	while( my ($f, $out_fh) = each %out_fh ){
		close $out_fh;
	}
	print "done.\n";
}

sub subcmd_concat ($$$$$) {
	my ($confdir, $inputdir, $outputdir, $buffereddir, $concatination) = @_;
	print "concatinating."    if     $concatination;
	print "no concatinating." unless $concatination;
	my $conffile = "$confdir/concatfilter.conf";
	my $conf   = Foucault::Configuration->new;
	my $filter = Foucault::Filter->new;

	my %buffer;
	$conf->read_concatfilter_conf( $conffile );
	my @rules = $conf->get_concatfilter_rules;
	$filter->set_concatbuffer( \%buffer );
        $filter->set_concatfilter_rules( @rules );

	my @list = listup_dir $inputdir;
	foreach my $f ( @list ){
		print ".";
		next unless $f =~ m"^(.+)\.ndjson$";
		my $tag = $1;
		open my $in_fh,  "<",  "$inputdir/$f"  or die;
		open my $out_fh, ">>", "$outputdir/$f" or die;
		while( my $in = <$in_fh> ){
			next if $in =~ m"^\s*$";
			my %in_hash   = json2hash $in;
			my $tag       = $in_hash{tag};
			my $messages  = [$in_hash{message}];
			my $cmessages = [];

			if( $concatination ){
				$filter->passthrough_concatfilter( $tag, $messages, $cmessages );
				foreach my $cmessage ( @$cmessages ){
					my %out_hash = ( %in_hash, 'message' => $cmessage );
					my $out_json = hash2json %out_hash;
					utf8::encode($out_json);
					print $out_fh "$out_json\n";
				}
			}else{
				print $out_fh $in;
			}
		}
		close $in_fh;
		close $out_fh;
	}
	while( my ($tag, $bufferunit) = each %buffer ){
		open my $out_fh, ">>", "$buffereddir/$tag.ndjson" or die;
		my $fracture = $$bufferunit{fracture};
		foreach my $message ( @$fracture ){
			my %out_hash = ( 'tag' => $tag, 'message' => $message );
			my $out_json = hash2json %out_hash;
			utf8::encode($out_json);
			print $out_fh "$out_json\n";
		}
		close $out_fh;
	}
	print "done.\n";
}

sub subcmd_sort_by_pattern ($$$$) {
	my ($confdir, $inputdir, $outputdir, $unmonitoreddir) = @_;
	print "sorting by pattern.";
	my $conffile  = "$confdir/anomalymonitor.conf";
	my $conf      = Foucault::Configuration->new;
	my $monitor   = Foucault::Monitor->new;
	my $wellknown = Foucault::WellKnown->new;

	$conf->read_anomalymonitor_conf( $conffile );
	my @rules = $conf->get_anomalymonitor_rules;
	$monitor->set_anomalymonitor_rules( @rules );
	$monitor->set_wellknown( $wellknown );

	my %out_fh;
	my @list = listup_dir $inputdir;
	foreach my $f ( @list ){
		print ".";
		next unless $f =~ m"^(.+)\.ndjson$";
		my $tag = $1;
		my ($name, $pattern, $pattern_regexp, $redirects, $captures_from_tag) = $monitor->get_anomalymonitor_rule($tag);
		open my $in_fh,          "<",  "$inputdir/$f" or die;
		my $unmonitored_fh;
		if( $pattern eq "" ){
			open $unmonitored_fh, ">>", "$unmonitoreddir/$f" or die;
		}
		while( my $in = <$in_fh> ){
			next if $in =~ m"^\s*$";
			my %in_hash   = json2hash $in;

			if( $pattern eq "" ){
				print $unmonitored_fh $in;
			}else{
				my $out_fh = $out_fh{$pattern};
				unless( defined $out_fh ){
					open $out_fh, ">>", "$outputdir/$pattern.ndjson" or die;
					$out_fh{$pattern} = $out_fh;
				}
				print $out_fh $in;
			}
		}
		close $in_fh;
		if( $pattern eq "" ){
			close $unmonitored_fh;
		}
	}
	while( my ($f, $out_fh) = each %out_fh ){
		close $out_fh;
	}
	print "done.\n";
}

sub subcmd_detect ($$$$$$) {
	my ($confdir, $wellknowndir, $inputdir, $anomalydir, $eventdir, $redirecteddir) = @_;
	print "detecting.";
	my $conffile  = "$confdir/anomalymonitor.conf";
	my $conf      = Foucault::Configuration->new;
	my $monitor   = Foucault::Monitor->new;
	my $wellknown = Foucault::WellKnown->new;

	$conf->read_anomalymonitor_conf( $conffile, $wellknowndir );
	my @rules = $conf->get_anomalymonitor_rules;
	$monitor->set_anomalymonitor_rules( @rules );
	$monitor->set_wellknown( $wellknown );
	$wellknown->set_path_of_wellknowndir( $wellknowndir );

	my @list = listup_dir $inputdir;
	foreach my $f ( @list ){
		print ".";
		next unless $f =~ m"^(.+)\.ndjson$";
		my $pattern = $1;

		$wellknown->read_patternregexp_file_of( $pattern );
		open my $in_fh,         "<",  "$inputdir/$f" or die;
		open my $anomaly_fh,    ">>", "$anomalydir/$f" or die;
		open my $event_fh,      ">>", "$eventdir/$f" or die;
		open my $redirected_fh, ">>", "$redirecteddir/$f" or die;
		while( my $in = <$in_fh> ){
			next if $in =~ m"^\s*$";
			my %in_hash       = json2hash $in;
			my $tag           = $in_hash{tag};
			my $message       = $in_hash{message};
			my $redirected_tag2cmessages = {};
			my $events        = [];
			my $unmonitored   = [];

			utf8::encode($message);
			$monitor->monitor_anomaly($tag, [$message], $redirected_tag2cmessages, $events, $unmonitored);

			die if @$unmonitored;

			unless( @$events ){
				# wellknown
				print $anomaly_fh "\n";
				print $event_fh   "\n";
			}else{
				foreach my $event ( @$events ){
					my $event_json = hash2json %$event;
					utf8::encode($event_json);
					if( $$event{event} =~ m"ANOMALY" ){
						print $anomaly_fh "$event_json\n";
						print $event_fh   "\n";
					}else{
						print $anomaly_fh "\n";
						print $event_fh   "$event_json\n";
					}
				}
			}
			while( my ($tag, $cmessages) = each %$redirected_tag2cmessages ){
				foreach my $cmessage ( @$cmessages ){
					my %redirected_hash = ( %in_hash, 'tag' => $tag, 'message' => $cmessage );
					my $redirected_json = hash2json %redirected_hash;
					utf8::encode($redirected_json);
					print $redirected_fh "$redirected_json\n";
				}
			}
		}
		close $in_fh;
	}
	print "done.\n";
}

sub subcmd_classify ($$$) {
	my ($confdir, $inputdir, $outputdir) = @_;
	print "classifing.";
	my $wellknown = Foucault::WellKnown->new;
	$wellknown->set_path_of_wellknowndir( $confdir );

	my @list = listup_dir $inputdir;
	foreach my $f ( @list ){
		print ".";
		next unless $f =~ m"^(.+)\.ndjson$";
		my $pattern = $1;
		$wellknown->read_generalizerules_file_of( $pattern );

		open my $in_fh,  "<",  "$inputdir/$f" or die;
		open my $out_fh, ">>", "$outputdir/$f" or die;
		while( my $in = <$in_fh> ){
			if( $in =~ m"^\s*$" ){
				print $out_fh "\n";
				next;
			}

			my %in_hash = json2hash $in;
			my $message = $in_hash{message};
			utf8::encode($message);
			my $generalized = $wellknown->generalize( $pattern, $message );
			utf8::decode($generalized);

			my %out_hash = ( %in_hash, 'generalized' => $generalized );
			my $out_json = hash2json %out_hash;
			utf8::encode($out_json);
			print $out_fh "$out_json\n";
			
		}
		close $in_fh;
		close $out_fh;
	}

	print "done.\n";
}

sub subcmd_sort_events ($$$$) {
	my ($confdir, $anomalydir, $eventdir, $outputdir) = @_;
	print "sorting events.";
	my $conffile = "$confdir/report.conf";
	my $conf      = Foucault::Configuration->new;
	my $diagnosis = Foucault::Diagnosis->new;
	$conf->read_report_conf( $conffile );
	my $rules = $conf->get_report_rules;
	$diagnosis->set_report_rules($rules);

	my %out_fh;
	my @list = listup_dir $anomalydir;
	foreach my $f ( @list ){
		print ".";
		next unless $f =~ m"^(.+)\.ndjson$";
		my $pattern = $1;

		open my $in_fh,  "<",  "$anomalydir/$f" or die;
		while( my $in = <$in_fh> ){
			next if $in =~ m"^\s*$";

			my %in_hash     = json2hash $in;
			my @eventgroups = $diagnosis->get_eventgroupnames_of( \%in_hash );
			foreach my $eventgroup ( @eventgroups ){
				my $out_fh = $out_fh{$eventgroup};
				unless( defined $out_fh ){
					open $out_fh, ">>", "$outputdir/$eventgroup.ndjson" or die;
					$out_fh{$eventgroup} = $out_fh;
				}
				print $out_fh $in;
			}
		}
		close $in_fh;
	}
	my @list = listup_dir $eventdir;
	foreach my $f ( @list ){
		print ".";
		next unless $f =~ m"^(.+)\.ndjson$";
		my $pattern = $1;

		open my $in_fh,  "<",  "$eventdir/$f" or die;
		while( my $in = <$in_fh> ){
			next if $in =~ m"^\s*$";

			my %in_hash     = json2hash $in;
			my @eventgroups = $diagnosis->get_eventgroupnames_of( \%in_hash );
			foreach my $eventgroup ( @eventgroups ){
				my $out_fh = $out_fh{$eventgroup};
				unless( defined $out_fh ){
					open $out_fh, ">>", "$outputdir/$eventgroup.ndjson" or die;
					$out_fh{$eventgroup} = $out_fh;
				}
				print $out_fh $in;
			}
		}
		close $in_fh;
	}

	while( my ($f, $out_fh) = each %out_fh ){
		close $out_fh;
	}
	print "done.\n";
}

sub subcmd_sort_anomalies ($$$) {
	my ($confdir, $inputdir, $outputdir) = @_;
	print "sorting anomalies.";
	my $conffile = "$confdir/report.conf";
	my $conf      = Foucault::Configuration->new;
	my $diagnosis = Foucault::Diagnosis->new;
	$conf->read_report_conf( $conffile );
	my $rules = $conf->get_report_rules;
	$diagnosis->set_report_rules($rules);

	my %out_fh;
	my @list = listup_dir $inputdir;
	foreach my $f ( @list ){
		print ".";
		next unless $f =~ m"^(.+)\.ndjson$";
		my $pattern = $1;

		open my $in_fh,  "<",  "$inputdir/$f" or die;
		while( my $in = <$in_fh> ){
			next if $in =~ m"^\s*$";

			my %in_hash     = json2hash $in;
			my @eventgroups = $diagnosis->get_eventgroupnames_of( \%in_hash );
			foreach my $eventgroup ( @eventgroups ){
				my $out_fh = $out_fh{$eventgroup};
				unless( defined $out_fh ){
					open $out_fh, ">>", "$outputdir/$eventgroup.ndjson" or die;
					$out_fh{$eventgroup} = $out_fh;
				}
				print $out_fh $in;
			}
		}
		close $in_fh;
	}

	while( my ($f, $out_fh) = each %out_fh ){
		close $out_fh;
	}
	print "done.\n";
}

sub subcmd_tabulate_events ($$$) {
	my ($confdir, $inputdir, $outputdir) = @_;
	print "tabulating events.";
	my $conffile = "$confdir/report.conf";
	my $conf      = Foucault::Configuration->new;
	my $diagnosis = Foucault::Diagnosis->new;
	$conf->read_report_conf( $conffile );
	my $rules = $conf->get_report_rules;
	$diagnosis->set_report_rules($rules);

	my @list = listup_dir $inputdir;
	foreach my $f ( @list ){
		print ".";
		next unless $f =~ m"^(.+)\.ndjson$";
		my $eventgroup = $1;

		my $total;
		my %subtotal_by_tag;
		my %subtotal_by_event;
		my %cross_tag_event;
		my %cross_pattern_event;
		my %cross_attrname_attrvalue;
		my %cross_attrname_attrvalue_event;

		open my $in_fh,  "<",  "$inputdir/$f" or die;
		while( my $in = <$in_fh> ){
			next if $in =~ m"^\s*$";

			my %in_hash = json2hash $in;
			my $tag     = $in_hash{tag};
			my $pattern = $in_hash{pattern};
			my $event   = $in_hash{event};

			$total++;
			$subtotal_by_tag  {$tag}++;
			$subtotal_by_event{$event}++;
			$cross_tag_event    {$tag}->{$event}++;
			$cross_pattern_event{$pattern}->{$event}++;

			my %attrs   = $diagnosis->get_attributes_of( \%in_hash );
			while( my ($k, $v) = each %attrs ){
				$cross_attrname_attrvalue{$k}->{$v}++;
				$cross_attrname_attrvalue_event{$k}->{$v}->{$event}++;
			}
		}
		close $in_fh;

		my $json = $JSON_CODEC_PRETTY->encode( {
			total		               => $total,
			subtotal_by_tag	               => \%subtotal_by_tag,
			subtotal_by_event 	       => \%subtotal_by_event,
			cross_pattern_event            => \%cross_pattern_event,
			cross_tag_event                => \%cross_tag_event,
			cross_attrname_attrvalue       => \%cross_attrname_attrvalue,
			cross_attrname_attrvalue_event => \%cross_attrname_attrvalue_event,
		} );
		open my $out_fh,  ">", "$outputdir/$eventgroup.json" or die;
		print $out_fh "$json\n";
		close $out_fh;
	}

	print "done.\n";
}

sub subcmd_tabulate_anomalies ($$$) {
	my ($confdir, $inputdir, $outputdir) = @_;
	print "tabulating anomalies.";
	my $conffile = "$confdir/report.conf";
	my $conf      = Foucault::Configuration->new;
	my $diagnosis = Foucault::Diagnosis->new;
	$conf->read_report_conf( $conffile );
	my $rules = $conf->get_report_rules;
	$diagnosis->set_report_rules($rules);

	my @list = listup_dir $inputdir;
	foreach my $f ( @list ){
		print ".";
		next unless $f =~ m"^(.+)\.ndjson$";
		my $eventgroup = $1;

		my %pattern2generalized2info;
		my @infos; 
		open my $in_fh,  "<",  "$inputdir/$f" or die;
		while( my $in = <$in_fh> ){
			next if $in =~ m"^\s*$";

			my %in_hash = json2hash $in;
			my $pattern     = $in_hash{pattern};
			my $message     = $in_hash{message};
			my $generalized = $in_hash{generalized};
			my $tag         = $in_hash{tag};

			my $info = $pattern2generalized2info{$pattern}->{$generalized};
			unless( defined $info ){
				$info = {
					"tag"         => $tag,
					"message"     => $message,
					"pattern"     => $pattern,
					"generalized" => $generalized,
					"times"       => 0,
				};
				$pattern2generalized2info{$pattern}->{$generalized} = $info;
				push @infos, $info;
			}
			$$info{times}++;

		}
		close $in_fh;

		my @ranking = sort { $$b{times} <=> $$a{times} || $$a{generalized} cmp $$b{generalized} } @infos;
		my $json = $JSON_CODEC_PRETTY->encode( \@ranking );
		utf8::encode($json);
		open my $out_fh,  ">", "$outputdir/$eventgroup.json" or die;
		print $out_fh "$json\n";
		close $out_fh;
	}

	print "done.\n";
}

sub subcmd_run_statsdir ($$) {
	my ($dir, $concatination) = @_;
	subcmd_sort_by_tag
		"$dir/inputs",
		"$dir/1-1_collection_by_tag";
	subcmd_concat
		"$dir/1_concatination_setting",
		"$dir/1-1_collection_by_tag",
		"$dir/1-2_concatinated_messages",
		"$dir/1-3_buffered_messages",
		$concatination;
	subcmd_sort_by_pattern
		"$dir/2_monitoring_setting",
		"$dir/1-2_concatinated_messages",
		"$dir/2-1_collection_by_pattern",
		"$dir/2-2_unmonitored_messages";
	subcmd_detect
		"$dir/2_monitoring_setting",
		"$dir/3_detection_setting",
		"$dir/2-1_collection_by_pattern",
		"$dir/3-1_detected_anomalies",
		"$dir/3-2_detected_events",
		"$dir/3-3_redirected_messages";
	subcmd_classify
		"$dir/4_classification_setting",
		"$dir/3-1_detected_anomalies",
		"$dir/4-1_generalized";
	subcmd_sort_events
		"$dir/5_tabulation_setting",
		"$dir/3-1_detected_anomalies",
		"$dir/3-2_detected_events",
		"$dir/5-1_grouped_events";
	subcmd_sort_anomalies
		"$dir/5_tabulation_setting",
		"$dir/4-1_generalized",
		"$dir/5-2_grouped_generalized";
	subcmd_tabulate_events
		"$dir/5_tabulation_setting",
		"$dir/5-1_grouped_events",
		"$dir/5-3_cross_tables";
	subcmd_tabulate_anomalies
		"$dir/5_tabulation_setting",
		"$dir/5-2_grouped_generalized",
		"$dir/5-4_classified_by_equivalence";
}

sub subcmd_compile_statsdir ($) {
	my ($dir) = @_;
	subcmd_compile
		"$dir/2_monitoring_setting",
		"$dir/4_classification_setting",
		"$dir/samples",
		"$dir/additional_samples",
		"$dir/3_detection_setting";
}

sub usage () {
	print	"usage: % $0 ACTION\n",
		"\n",
		"    ACTION\n",
		"	create DIR		create working directory for diagnosis.\n",
		"	cleanup DIR\n",
		"	run DIR			diagnose input files.\n",
		"	compile DIR		build pattern regexp files from samples.\n",
		"\n";
	exit 1;
}

STDOUT->autoflush(1);
my $subcmd = shift @ARGV;
if    ( $subcmd eq 'create' ){
	my $r = GetOptions( );
	my $d = $ARGV[0];
	usage if $d eq '';
	subcmd_create_statsdir $d;

}elsif( $subcmd eq 'cleanup' ){
	my $r = GetOptions( );
	my $d = $ARGV[0];
	usage if $d eq '';
	unless( -d $d ){ print "$d: working directory cannot be found.\n"; exit 2; }
	subcmd_cleanup_statsdir $d;

}elsif( $subcmd eq 'run' ){
	my $concatination = 1;
	my $r = GetOptions('concatination!' => \$concatination,  );
	my $d = $ARGV[0];
	usage if $d eq '';
	unless( -d $d ){ print "$d: working directory cannot be found.\n"; exit 2; }
	subcmd_run_statsdir $d, $concatination;

}elsif( $subcmd eq 'compile' ){
	my $r = GetOptions( );
	my $d = $ARGV[0];
	usage if $d eq '';
	unless( -d $d ){ print "$d: working directory cannot be found.\n"; exit 2; }
	subcmd_compile_statsdir $d;

}else{
	usage;
}

exit 0;

